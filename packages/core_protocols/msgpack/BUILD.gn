#
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0.
# See the LICENSE file for more information.
#
import("//build/feature/ten_package.gni")
import("//build/ten_runtime/feature/publish.gni")
import("//build/ten_runtime/glob.gni")
import("//build/ten_runtime/options.gni")

ten_runtime_glob("msgpack_files") {
  file_list = []

  include_dirs = [
    "//packages",
    "//core",
  ]
  deps = [
    "common",
    "msg",
    "//core/src/ten_runtime:ten_runtime_library",
    "//third_party/msgpack:msgpackc",
  ]
}

ten_runtime_glob("msgpack_files_for_builtin_protocol") {
  file_list = [ "protocol.c" ]

  include_dirs = [
    "//packages",
    "//core",
  ]
  deps = [
    "common",
    "msg",
    "//core/src/ten_runtime:ten_runtime_library",
    "//third_party/msgpack:msgpackc",
  ]
}

ten_package("msgpack") {
  package_kind = "protocol"
  enable_build = true

  resources = [
    "manifest.json",
    "property.json",
  ]

  docs_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                           [
                             "--dir",
                             rebase_path("docs/**/*"),
                             "--dir-base",
                             rebase_path("docs"),
                             "--recursive",
                             "--only-output-file",
                           ],
                           "json")

  foreach(docs_file, docs_files) {
    docs_file_rel_path = docs_file.relative_path
    resources += [ "docs/${docs_file_rel_path}=>docs/${docs_file_rel_path}" ]
  }

  sources = [ "protocol.c" ]
  include_dirs = [
    "//core",
    "//packages",
  ]
  deps = [
    "common",
    "msg",
    "//core/src/ten_runtime:ten_runtime_library",
    "//third_party/msgpack:msgpackc",
  ]

  # On Windows with MinGW, msgpack package calls TEN_MSGPACK_API functions
  # (e.g., ten_msgpack_deserialize_msgs, ten_msgpack_serialize_msgs) which are
  # defined in the same package (msg.c). Since these functions are compiled as
  # part of the msgpack DLL, they need to use actual symbols instead of
  # __declspec(dllimport) to avoid "undefined reference" errors.
  #
  # Why only MinGW needs this (not MSVC/Linux/macOS):
  #
  # 1. Linux/macOS: Use __attribute__((visibility("default"))) instead of
  #    __declspec(dllimport). No __imp_ prefix is generated, symbols are resolved
  #    directly. See msgpack_config.h.
  #
  # 2. MSVC: The MSVC linker can automatically resolve __declspec(dllimport) symbols
  #    even when they're in the same DLL. MSVC's linker is more lenient and can use the
  #    actual symbol definition from the same DLL.
  #
  # 3. MinGW (GCC on Windows): When GCC sees __declspec(dllimport), it generates code
  #    that references the symbol with an __imp_ prefix (e.g.,
  #    __imp_ten_msgpack_deserialize_msgs). This __imp_ prefix is used for importing
  #    symbols from external DLLs via import libraries. However, when linking the same
  #    DLL internally, the linker only has the actual symbol
  #    (ten_msgpack_deserialize_msgs) available, not the __imp_ prefixed version.
  #    This causes "undefined reference to __imp_..." errors.
  #
  # Solution: Define TEN_MSGPACK_EXPORT when compiling msgpack so it uses
  # __declspec(dllexport) instead of __declspec(dllimport), making it reference the
  # actual symbol directly.
  #
  # References:
  # - GCC dllimport behavior: https://gcc.gnu.org/onlinedocs/gcc-4.7.0/gcc/Function-Attributes.html
  if (is_win && is_mingw) {
    defines = [ "TEN_MSGPACK_EXPORT" ]
  }
}

if (ten_enable_ten_manager) {
  ten_package_publish("upload_protocol_msgpack_to_server") {
    base_dir = rebase_path("${root_out_dir}/ten_packages/protocol/msgpack")
    deps = [ ":msgpack" ]
  }
}
