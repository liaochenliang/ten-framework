#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/options.gni")

all_native_files = [
  "*.c",
  "*.cc",
  "*.cpp",
  "*.h",
  "*.hpp",
  "*.hh",
  "*.s",
  "*.S",
  "*.asm",
  "*.m",
  "*.mm",
]

template("ten_runtime_glob") {
  assert(defined(invoker.file_list) || invoker.file_list == [],
         "file_list is not defined")

  source_set(target_name) {
    l = []
    foreach(f, invoker.file_list) {
      l += [
        "--dir",
        rebase_path(f),
      ]
    }

    sources_info =
        exec_script("//.gnfiles/build/scripts/glob_file.py", l, "json")

    sources = []
    foreach(source_info, sources_info) {
      sources += [ source_info.path ]
    }

    if (defined(invoker.sources)) {
      sources += invoker.sources
    }

    include_dirs = ten_runtime_common_includes
    if (defined(invoker.include_dirs)) {
      include_dirs += invoker.include_dirs
    }

    defines = ten_runtime_common_defines
    if (defined(invoker.defines)) {
      defines += invoker.defines
    }

    # On Windows (with MinGW), when compiling test client executables that use
    # msgpack_client functions (e.g., ten_test_msgpack_tcp_client_create), we
    # need to define TEN_MSGPACK_EXPORT so that TEN_MSGPACK_API expands to
    # __declspec(dllexport) instead of __declspec(dllimport).
    # This is because static link (to msgpack functions) is used for tests,
    # instead of through dll (which needs import-export cooperation). In this
    # case, no "__imp_" prefix is involved, and all compile units should use
    # TEN_MSGPACK_EXPORT to prevent MinGW from generating __imp_ prefixed symbol
    # references that would cause "undefined reference" errors.
    #
    if (is_win && is_mingw) {
      defines += [ "TEN_MSGPACK_EXPORT" ]
    }

    if (defined(invoker.configs)) {
      configs += invoker.configs
    }

    deps = common_deps
    if (defined(invoker.deps)) {
      deps += invoker.deps
    }

    public_deps = common_public_deps
    if (defined(invoker.public_deps)) {
      public_deps += invoker.public_deps
    }

    libs = common_libs
    if (defined(invoker.libs)) {
      libs += invoker.libs
    }

    cflags = common_cflags
    if (defined(invoker.cflags)) {
      cflags += invoker.cflags
    }

    if (enable_coverage) {
      assert(is_linux && target_cpu == "x64",
             "Coverage instrumentation should only be enabled in Linux + x64")

      if (is_clang) {
        cflags += [
          "-fprofile-instr-generate",
          "-fcoverage-mapping",
        ]
      } else {
        # GCC
        cflags += [ "--coverage" ]
      }
    }

    cflags_c = common_cflags_c
    if (defined(invoker.cflags_c)) {
      cflags_c += invoker.cflags_c
    }

    if (enable_coverage) {
      assert(is_linux && target_cpu == "x64",
             "Coverage instrumentation should only be enabled in Linux + x64")

      if (is_clang) {
        cflags_c += [
          "-fprofile-instr-generate",
          "-fcoverage-mapping",
        ]
      } else {
        cflags_c += [ "--coverage" ]
      }
    }

    cflags_cc = common_cflags_cc
    if (defined(invoker.cflags_cc)) {
      cflags_cc += invoker.cflags_cc
    }

    if (enable_coverage) {
      assert(is_linux && target_cpu == "x64",
             "Coverage instrumentation should only be enabled in Linux + x64")

      if (is_clang) {
        cflags_cc += [
          "-fprofile-instr-generate",
          "-fcoverage-mapping",
        ]
      } else {
        cflags_cc += [ "--coverage" ]
      }
    }

    cflags_objc = common_cflags_objc
    if (defined(invoker.cflags_objc)) {
      cflags_objc += invoker.cflags_objc
    }

    cflags_objcc = common_cflags_objcc
    if (defined(invoker.cflags_objc)) {
      cflags_objcc += invoker.cflags_objcc
    }
  }
}
