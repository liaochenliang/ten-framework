#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/glob.gni")
import("//build/ten_runtime/ten.gni")
import("//third_party/curl/output_libs.gni")

ten_runtime_glob("msgpack_client") {
  file_list = [
    "msgpack_tcp.c",
    "tcp.c",
  ]
  deps = [ "//third_party/msgpack:msgpackc" ]
  include_dirs = [
    "//packages",
    "//tests/ten_runtime",
  ]

  # TODO(nzh): add a MSGPACK_API to control the export of msgpack symbols
  # Instead of using TEN_RUNTIME_API

  # On Windows with MinGW, msgpack_client functions (e.g.,
  # ten_test_msgpack_tcp_client_create, ten_test_msgpack_tcp_client_destroy) are
  # defined in the same package (msgpack_tcp.c). Since these functions are
  # compiled as part of the test client executable, they need to use actual
  # symbols instead of __declspec(dllimport) to avoid "undefined reference"
  # errors.
  #
  # Why only MinGW needs this (not MSVC/Linux/macOS):
  #
  # 1. Linux/macOS: Use __attribute__((visibility("default"))) instead of
  #    __declspec(dllimport). No __imp_ prefix is generated, symbols are resolved
  #    directly.
  #
  # 2. MSVC: The MSVC linker can automatically resolve __declspec(dllimport)
  #    symbols even when they're in the same executable. MSVC's linker is more
  #    lenient and can use the actual symbol definition from the same executable.
  #
  # 3. MinGW (GCC on Windows): When GCC sees __declspec(dllimport), it generates
  #    code that references the symbol with an __imp_ prefix (e.g.,
  #    __imp_ten_test_msgpack_tcp_client_create). This __imp_ prefix is used for
  #    importing symbols from external DLLs via import libraries. However, when
  #    linking the same executable internally, the linker only has the actual
  #    symbol (ten_test_msgpack_tcp_client_create) available, not the __imp_
  #    prefixed version. This causes "undefined reference to __imp_..." errors.
  #
  # Solution: Define TEN_RUNTIME_EXPORT when compiling msgpack_client so it uses
  # __declspec(dllexport) instead of __declspec(dllimport), making it reference
  # the actual symbol directly.
  if (is_win && is_mingw) {
    defines = [ "TEN_RUNTIME_EXPORT" ]
  }
}

ten_runtime_glob("client") {
  file_list = []
  deps = [ ":msgpack_client" ]

  if (ten_enable_curl) {
    file_list += [
      "curl_connect.c",
      "http.c",
    ]

    if (curl_use_shared_lib == false) {
      # Please refer to https://github.com/curl/curl/issues/2196
      defines = [ "CURL_STATICLIB" ]
    }

    deps += [ "//third_party/curl" ]
  }

  include_dirs = [
    "//packages",
    "//tests/ten_runtime",
  ]
}
